label din_winterlong_story:
    $ din_set_mode_adv()
    stop music fadeout 3
    $ renpy.pause(2, hard=True)
    $ renpy.block_rollback()
    $ persistent.sprite_time = "night"
    $ persistent.timeofday = "night"
    $ din_set_timeofday_cursor_var = True
    $ din_story_intro('Длиною в зиму\nДень Третьего', 'night', 'bg din_fireplace_winterlong_anim', 'din_third smile', 'Длиною в зиму', 'День Третьего', 'forest_night')
    $ renpy.pause(2, hard=True)
    $ persistent.sprite_time = "sunset"
    $ persistent.timeofday = "sunset"
    scene bg din_int_dining_hall_people_sunset with Dissolve(2)
    play ambience ambience_dining_hall_full fadein 3
    din_narrator "Обычный, непримечательный день."
    din_narrator "Я прохлаждался на галёрке стандартного сценария какого-то Пионерчика. Он романтично болтал с двумя красноволосыми за столом у окна."
    scene bg din_food_normal_sunset with dissolve
    din_narrator "До этого я наблюдал из массовки за местным ритмом жизни, но сейчас просто лениво рассматривал свою порцию еды."
    din_narrator "По факту, к еде можно и не притрагиваться. При должной сноровке всю неделю несложно протянуть и без еды. Книги говорят, что протянуть можно около месяца, но для меня такая информация бесполезна."
    scene bg din_int_dining_hall_people_sunset with dissolve
    din_narrator "Я подпер кулаком голову."
    din_narrator "Вмешаться в события этого лагеря или молча наблюдать дальше?"
    stop ambience fadeout 2
    $ renpy.pause(1, hard=True)
    play sound sfx_open_door_kick
    play music din_dance_with_the_dead_eyes_of_madness fadein 2
    show din_hall pos1 devil_smile far at center with dissolve
    din_narrator "От решения меня отвлек новый гость, рывком открывший дверь. Кроме двух ножей в руках - ничего примечательного."
    din_pi_teapot "Девочки и мальчики, берегите ваши руки, ноги, пальчики! {w}А еще лучше - просто испаритесь на счет три."
    din_narrator "Куклы застыли в недоумении, а вот местный Пионер быстро начал собираться в окно. {w}Смышлёный парень. Знает, что с незнакомцами, у которых есть нож, лучше не разговаривать."
    din_pi_teapot "Три."
    din_narrator "Куклы всё еще сидели, раскрыв рты. Пионер успел улизнуть."
    din_pi_teapot "Ну кто бы сомневался."
    din_pi_teapot "Лады, давайте по старинке."
    play sound din_knife_fly
    $ renpy.pause(0.5, hard=True)
    play sound sfx_broken_dish
    din_narrator "Быстрым движением гость выкинул нож, который прочертил прямую траекторию и просвистел в паре сантиметров от головы Алисы."
    din_narrator "Потом снаряд с лязгом упал на пол, а метатель грозно поигрывал в руке вторым."
    din_narrator "Куклы сменили смятение на панику в своих пустых глазах. Смекнули, что сейчас будет нечто не очень хорошее."
    scene bg din_int_dining_hall_sunset_crashed with fade
    play ambience din_crowd_ambience fadein 2
    din_narrator "Они начали резко вставать, опрокидывать стулья, и панически искать пути побега."
    din_pi_teapot "Выход на кухне, если кому интересно."
    din_narrator "С занудным тоном бросил гость."
    din_narrator "Толпа не задумываясь бросилась бежать за стойку раздачи."
    stop ambience fadeout 2
    $ renpy.pause(1, hard=True)
    play ambience ambience_dining_hall_empty fadein 2
    din_narrator "Скоро остались только я, гость и мой остывший ужин."
    din_narrator "У меня не было моральных сил убегать куда-то. Кажется, остается только один выход."
    din_narrator "Ненавижу насилие."
    show din_hall pos2 normal at center with dissolve
    din_narrator "Гость, похоже, собирался уходить, но заметил, что какая-то дефектная кукла еще сидит в столовой. {w}Повернув лезвие ножа в мою сторону, он начал аккуратно подходить."
    din_th "Бросить в него стул или побежать по столам? {w}Можно еще осколок стекла раздобыть..."
    stop music fadeout 3
    din_narrator "Между нами осталось с десяток метров. Я приготовился к броску."
    show din_hall pos2 smile with dspr
    din_pi_teapot "Волгу мне в столовую, Третий! {w}Ты!"
    din_third "Аааа, Чайник... {w}Привет."
    din_narrator "Я сел обратно за свой трапезный стол."
    din_teapot "Третий, я ради тебя полмира обошел! Я придумал десять разных фраз для врывов в столовые! Какого черта ты торчишь непонятно где?"
    din_narrator "Ну начина-а-ается."
    din_third "Может быть, хотел недельку тишины. Как ты меня вообще нашел?"
    din_teapot "Шел по зову своего сердца. Оно сразу указало верный путь. {w}Пришлось обойти всего-то двенадцать лагерей."
    din_third "Почему искал меня именно в столовой? Я мог быть буквально где угодно."
    din_teapot "Ой, не будь таким доверчивым, Третий. Я сюда за мукой зашел. {w}Но тебя тоже рад видеть, ты не подумай."
    din_third "Мука в столовой?"
    din_teapot "Звучит дико, да? {w}Она не только в библиотеке есть. А мне понадобится еще много."
    din_third "Что ты теперь задумал?"
    din_teapot "Что {b}Я{/b} задумал? Под насколько большим камнем ты жил, Третий? В конце этой смены Неделя Зимы! И раз ты уже здесь и ничем не занят, то мне понадобится твоя грубая сила."
    din_th "Ах да, эта чушь..."
    din_third "Не сочти за грубость, но плевать я хотел на этот праздник и на вашу Зиму."
    show din_hall pos2 sad with dspr
    din_teapot "Да не будь ты старым нудным пнём, Третий. Тебе там тоже найдется чем заняться. Тем более праздник для всех."
    din_teapot "Даже Ниточник будет."
    din_third "Он что, согласился помочь с организацией?"
    show din_hall pos2 smile with dspr
    din_teapot "Пха-ха-ха! Нет конечно, он же не дурачок какой-нибудь."
    din_third "Мне нужно напоминать, что ты-то на это подписался?"
    din_teapot "Ну, мне не жалко. {w}Я огнеупорный."
    show din_hall pos2 smile2 with dspr
    din_narrator "Чайник счастливо улыбнулся."
    din_third "Точно сказано."
    show din_hall pos2 sad with dspr
    din_narrator "Он посмотрел на меня пристально и укоризненно."
    din_third "Хорошо-хорошо. Ты огнеупорный."
    din_narrator "Я начал глазами искать что-то воспламенимое в этой столовой."
    stop ambience fadeout 2
    scene bg black with Dissolve(2)
    $ renpy.pause(1, hard=True)
    $ renpy.block_rollback()
    $ persistent.timeofday = "night"
    $ persistent.sprite_time = "night"
    scene bg din_fireplace_winterlong_anim with Dissolve(2)
    play sound_loop din_voices fadein 2
    play ambience sfx_forest_fireplace fadein 2
    play music din_god_is_an_astronaut_first_day_of_sun fadein 5
    din_narrator "С высоты дерева было хорошо видно поляну, покрытую смесью муки, сахара и штукатурки."
    din_narrator "Не слишком равномерно, но с земли это было мало заметно."
    din_narrator "Я высыпал последний мешок по ветру, закончив украшать локацию."
    din_teapot "Отлично, Третий! Можешь слезать. Скоро начнут подтягиваться все остальные!"
    din_narrator "Я бережно спрыгнул на землю."
    din_narrator "Остальные тоже заканчивали свою часть. В центре постепенно разгоралось кострище, вокруг него расходились скромные места, чтобы хватило всем желающим. В отдалении находилась небольшая деревянная платформа для выступлений."
    din_narrator "Зимний вид поляны навевал слабознакомое ощущение. Неделя Зимы проводится очень редко. Многие даже не помнят, что когда-то были на ней."
    play sound sfx_bush_body_fall
    din_narrator "Я откинулся, коснувшись спиной холодной земли."
    scene bg din_stars_bush_anim with fade
    din_narrator "Только я и еще пара-тройка Пионеров никогда не забывают традицию. Сейчас еще Чайник помнит, но это вряд ли надолго."
    din_narrator "Из-за такой редкости Неделя Зимы была чем-то невероятным, даже удивительным для новичков. А помнящие лишь завидуют этим давно забытым эмоциям."
    play sound sfx_wind_gust
    $ renpy.pause(1.5, hard=True)
    din_narrator "Прохладно. Интересно, так ли представляли зиму придумавшие Внешний Мир фантазёры?"
    din_narrator "Чайник вместе с двумя знакомыми подкинул пару дров в костёр. Огонь довольно затрещал."
    din_narrator "Костер был идеально собран, чтобы всем хватило места вокруг него, но согреться было сложно."
    din_narrator "Один за другим из леса появлялись Пионеры. Кто-то — группой, другие — в одиночку, внимательно смотря по сторонам."
    din_narrator "Все друг за другом молча занимали места у очага. Новенькие нервно смотрели по сторонам, а мои сверстники расслабленно откидывались назад, закрыв глаза и наслаждаясь холодом ночи."
    din_narrator "Некоторые даже спустя столько циклов верят, что когда-то жизнь была другой, что Внешний Мир — не плод чьей-то давно поломанной фантазии."
    din_narrator "Может, эта надежда дает им держать себя в руках. Пускай. Каждый сам вправе решать, во что ему верить."
    din_narrator "Смешно, что это, по большей части, праздник надежды, хотя поддерживают его те, кто от надежды давно отказался."
    din_th "У вечности определённо есть чувство юмора."
    $ renpy.pause(3, hard=True)
    din_narrator "Всё сложнее следить за ходом времени. Особенно в таких... Ситуациях."
    scene bg black with Dissolve(2)
    $ renpy.pause(1, hard=True)
    scene bg din_fireplace_winterlong_anim with Dissolve(2)
    din_narrator "Когда я поднялся, то большинство мест у костра пустовало. Все занялись более «живыми» занятиями."
    din_narrator "В другом конце поляны красовалась состоящая из пары досок барная стойка с изрядным запасом Столичной. Многим было сложно справиться с эмоциями."
    din_narrator "Небольшая группа любопытствующих пошла послушать речи исследователей."
    stop music fadeout 3

    menu:
        "К стойке":
            jump din_winterlong_story_bar

        "Подойти к учёным": 
            jump din_winterlong_story_science   
    
label din_winterlong_story_bar:
    din_th "Возможно, стоило бы отказаться от алкоголя. {w}Хотя бы на эмоциональные праздники."
    din_th "С другой стороны, сколько выпивка вызывает истерик, столько и останавливает."
    din_th "Да и не в моих это привычках - решать за других."
    stop ambience fadeout 2
    scene bg din_ext_bar_night 
    show din_nuts silhouette normal at center
    with dissolve
    play ambience ambience_forest_night fadein 2
    play music din_el_huervo_daisuke fadein 2
    din_narrator "Заправлявший баром Пионер молча налил мне стакан."
    din_narrator "Как раз на фоне костра сейчас пара расслабленных, но энергичных Чайников выслушивала слова от фигуры со знакомо наклонённой головой."
    din_th "А у них отлично получается."
    din_narrator "Бармен же не делал никаких лишних движений."
    din_th "Вряд ли этим бы занимался кто-то кроме любителя посмотреть на чужую печаль."
    din_th "Или... {w}Я ошибаюсь? {w}Буду надеяться, что осталось хоть что-то, в чём я могу ошибаться."
    hide din_nuts silhouette normal with dissolve
    din_narrator "Забрав стакан, я отсел за стол к одинокому Пионеру, безвольно допивавшему очередную бутылку."
    din_narrator "Вот эту-то личность я хорошо знал."
    din_narrator "Огонёк когда-то был невероятным. Он излучал счастье и радость, чуть ли не единственный."
    din_narrator "Не безумное счастье Чайника, не садистическую радость всяких слабаков, а обычное счастье, каким его описывают в книгах Внешнего Мира."
    din_narrator "С ним было что-то определённо не так. Я хотел понять, что именно, поэтому мы провели слишком много времени в одной компании."
    din_narrator "А потом в одну прекрасную смену Огонёк всё забыл. {w}Встретил меня паникой, страхом, не помнил ничего про других."
    din_narrator "Стал «новичком»."
    din_narrator "Такое случается с каждым. С разной периодичностью, но все возвращаются в начало мыслей."
    din_narrator "Сейчас только я и Ниточник, кажется, еще не «омолаживались». Мы помним всех остальных, но не друг друга."
    din_narrator "Возможно, мы восстановились в одну смену."
    din_narrator "В любом случае, наша очередь - только вопрос времени."
    din_narrator "Наш срок изменений может стремиться к бесконечности, но... {w}Мы здесь. {w}Бесконечность непременно наступит."
    din_narrator "Мой поток мыслей прервали негромкие шаги рядом." 
    show din_gensek silhouette normal at center with dissolve
    din_narrator "Кто-то проходил мимо, но, заметив меня, придвинул соседний стул и сел к нам."
    din_th "Кто настолько смелый, чтобы подсесть без приглашения?"
    din_narrator "Я уже размышлял как бы убрать новоприбывшего предельно болезненно, при этом без табуированного сегодня прямого насилия, но неприятное узнавание заставило пересмотреть планы."
    show din_gensek stay normal with dissolve
    din_narrator "Генсек. Если я третий по опасности Пионер, то Генсек второй, причем на голову меня выше."
    din_narrator "Выше него только Чокнутый, но эти двое слишком хорошо ладят, чтобы всерьёз угрожать друг другу."
    din_gensek "Какая встреча, Третий."
    din_narrator "Говорил Генсек всегда ровно, спокойно и уверенно. {w}Как и полагается его статусом."
    din_third "И ты здравствуй. Зашел к нам опохмелиться?"
    show din_gensek stay smile with dspr
    din_narrator "Генсек тихо улыбнулся."
    din_gensek "Не совсем. Но спасибо за предложение."
    show din_gensek stay normal with dspr
    din_gensek "Пропущу этап любезностей, если ты не возражаешь."
    din_gensek "Не знаю, слышал ли ты, но через две смены пляжная вечеринка."
    din_third "Это точно не ко мне."
    din_th "Валяться под солнцем, изредка плавая и бросая мяч? Может, моё мнение и не распростарнённое, но это - трата времени. {w}И такие слова кое-что да значат в наших масштабах."
    din_gensek "Там пройдет небольшой морской конфликт и нам с Чокнутым не хватает рук."
    din_third "Хотите завербовать меня к себе?"
    din_gensek "Нет, конечно. У конфликта три стороны, и нам не хватает еще одного приемлемого соперника."
    din_third "Польщен. Но это не грубое один на один. Нужна не только сноровка, но и изобретательность."
    din_gensek "Она и в грубом один на один нужна. Ты отлично подходишь."
    din_third "Предложи это Чайнику. Он обрадуется."
    din_narrator "Мой собеседник на треть секунды посмотрел в сторону, представляя себе картину."
    show din_gensek point smile with dspr
    din_gensek "Хорошая идея. Но было бы неплохо, если бы корабль противника не пошел на дно в самом начале под весом собственной «изобретательности»."
    din_narrator "В этом я не мог не согласиться. Соревнование действительно получилось бы... {w}разочаровывающим."
    din_gensek "Можешь взять его в команду. Вы отлично сработаетесь."
    din_third "А если я откажусь? Что тогда?"
    show din_gensek stay normal with dspr
    din_gensek "Да ничего нового, Третий. Мы с Чокнутым проявим злопамятность, только и всего."
    din_third "Хоть Чокнутый и несравнимо сильнее меня, это не значит, что я боюсь боли."
    show din_gensek stay smile with dspr
    din_gensek "Как забавно вы все делаете из него непобедимого дьявола. {w}{b}Ты{/bg} боли не боишься. А вот твои знакомые...."
    din_third "Будешь пытать Чайника только чтобы позлить меня?"
    din_gensek "О, нет. Смотри шире."
    din_gensek "Пока он будет занят, ты сможешь переключиться на кого-то другого. Так что втягивать его в нашу небольшую политику бесполезно."
    din_gensek "Но втянуть можно не его одного. Представь — в общем лагере висит обьявление, что каждый, кто заговорит с тобой, подписывается на наше плохое отношение."
    din_gensek "Даже если кто-то имеет высокий болевой порог или безумное самовнушение, то его образумят знакомые, пригрозив изоляцией. Не прими за оскорбление, но не думаю, что ты стоишь таких жертв."
    din_narrator "Идея была крайне хороша. Она не могла меня не удивить своей продуманностью."
    din_third "Я тебя услышал."
    din_narrator "Старое, почти забытое чувство собственной беспомощности перед другими. {w}Хотя нет. Я всегда могу отказаться. Нужно лишь быть готовым к последствиям."
    show din_gensek stay smile with dspr
    din_gensek "Вот и славно."
    din_third "Куда ты вообще шел до того, как я тебя отвлёк?"
    din_narrator "Хотелось перевести тему."
    din_gensek "Хотел зайти сюда за Чокнутым."
    din_third "За столами и с той, и с этой стороны, кроме нас никого нет."
    din_gensek "Я знаю."
    din_narrator "Он произнёс это чуть удивлённо, словно я сказал бесполезную информацию."
    din_gensek "Что же, мне, как ты заметил, пора. {w}Веселитесь. Неделя Зимы же."
    hide din_gensek with dissolve
    din_narrator "Сказал он, как какой-то праздничный дух, и оставил наш стол."
    din_narrator "А ведь Генсек прав."
    din_narrator "Всё равно желания еще хоть что-то делать уже не было."
    din_narrator "Я взял одну из немногих еще не открытых бутылок Огонька и, открыв, поднёс её ко рту."
    stop music fadeout 4
    stop ambience fadeout 4
    stop sound_loop fadeout 4
    scene bg black with Dissolve(2)
    $ persistent.din_flags['din_gensek_info_received'] = True
    $ renpy.pause(2, hard=True)
    jump din_winterlong_story_interlude
    
label din_winterlong_story_science:
    din_narrator "Почему бы и нет?"
    stop ambience fadeout 2
    scene bg din_ext_scene_night with dissolve
    play ambience ambience_forest_night fadein 2
    play music din_h1_timeless fadein 5
    din_narrator "Вопреки моим ожиданиям, пылких споров там не происходило. Все сдержанно слушали мысли стоящего на платформе."
    din_narrator "Я тихо встал сбоку, чтобы не привлекать внимание."
    show din_hall pos2 smile2 far at center with dissolve
    din_narrator "Вещал, что не удивительно, Чайник."
    din_teapot "...да, как вы и могли догадаться, вопрос в источнике энергии."
    din_teapot "Не важно, дрейфуем мы в абсолютном вакууме или находимся на экспериментальном полигоне, в любом случае нужно невообразимое колличество энергии, чтобы обеспечить во всех параллелях тепло."
    din_narrator "За столько времени я так привык к глупостям Чайника, что перестал воспринимать его как, без преувеличения, оплот рационализма и науки во всеобщих глазах."
    din_teapot "Рассмотрим несколько моих версий. Когда я закончу, пожалуйста, озвучьте свои."
    din_teapot "Итак, первый вариант - события берут место в чьём-то из наших воображений."
    din_teapot "Тут всё просто - законы физики могут нарушаться в любую сторону. Есть доказательства и за, и против этой теорий, но тема старая, их вы уже наверняка слышали, не буду затягивать."
    din_teapot "Второй вариант - есть источники тепла, поэтому где-то жарче, где-то, наоборот, прохладнее."
    din_teapot "Таких источников может быть около десятка, плюс местное Солнце."
    din_teapot "Оно вряд ли находится дальше пяти километров над землей, из чего можно сделать приблизительные рассчеты."
    din_pi_generic "Пять километров - длина дороги?"
    din_teapot "Да, почти. Это половина длины. Я всё еще надеюсь, что каждая параллель ограничена сферой. {w}Если создавшие эти мирки могли менять пространство, но не знали о банальном удобстве сферических структур... {w}То у нас проблемы."
    din_teapot "Рассчеты я, разумеется, уже провёл. {w}В очень сухой теории источниками могут служить крохотные ядерные синтезы. {w}Молекулярные, можно сказать. {w}В теории, потому что я их не нашел."
    din_teapot "Но их нужно как-то запускать и координировать. Возможно, какими-то волнами."
    din_teapot "Поэтому попрошу вас в течение следующих двух недель проследить за поведением радио и самоделок в разных участках ваших лагерей. "
    din_teapot "Список мест напишу и оставлю в общей столовой. Результаты идеально оформить в письменном виде. Сложите там же, в столовой, возле доски обьявлений."
    din_teapot "Последний вариант: источник энергии - мы сами, но в разных местах реагируем и действуем иначе. Отсюда и разница температур в разных точках."
    din_teapot "Эту теорию у меня пока не было желания рассмотреть, поговорим о ней после результатов. {w}На этом всё, спасибо."
    hide din_hall with dissolve
    din_narrator "Он в тишине сошел с трибуны. Поискав свободное место, заметил меня."
    show din_hall pos2 smile at center with dissolve
    din_teapot "О, какая компания! Ты вовремя. {w}Сыграем в города? {w}Райцентр!"
    din_third "Райцентр."
    din_teapot "..."
    din_teapot "Ладно, ты выиграл."
    din_third "До тебя рассказывали что-то интересное?"
    din_teapot "Да нет, ничего такого, что бы ты не предполагал раньше. {w}Настолько ты умён, Третий."
    din_third "Слишком уж топорный комплимент. Я от тебя ждал большего."
    din_teapot "Не всегда же мне алмазами мысли сыпать? Иногда надо брать перерыв. {w}Не хочешь выступить?"
    din_narrator "Я еще раз бросил взгляд на платформу. Новый лектор сейчас рисовал несколько систем координат. {w}Искренне надеюсь, что это не четырёхмерность."
    din_third "Никогда не буду этим заниматься."
    din_teapot "Никогда? От кого я это слышу. Это может убить пару дней твоего времени. А если повезёт, то и пару смен, задумайся!"
    din_third "Не хочу прикасаться к системе."
    din_teapot "О, боишься найти выход?"
    din_third "Боюсь, что могу нечто подвинуть и нас сплющит в единственную микроскопически маленькую точку пространства. Узнать вас поближе я и так могу."
    din_teapot "Разве не ты говорил, что система безупречна и нам на неё не повлиять?"
    din_third "Да, говорил..."
    din_teapot "Но ты еще и уверен, что можно что-то поломать."
    din_third "Я... {w}Так, Чайник, оставь эту тему в покое! Свои алмазы мыслей держи для других."
    din_third "Лагерь - лучшее, что случилось с нами."
    din_teapot "...единственное, что случилось с нами."
    din_third "Не вижу противоречия."
    din_teapot "Да, да, противоречия нет, никто не спорит."
    din_narrator "Он примирительно поднял руки вверх."
    $ renpy.pause(2, hard=True)
    din_narrator "Его взгляд проскользил где-то над моей головой, за спину."
    din_teapot "О, это тебя обрадует!"
    din_third "Очень в этом сомневаюсь."
    stop music fadeout 2
    stop ambience fadeout 2
    scene bg din_fireplace_winterlong_anim with dissolve
    play ambience sfx_forest_fireplace fadein 2
    din_narrator "Когда я развернулся, ничего сначала не бросилось в глаза."
    din_narrator "Вернее, не было понятно где искать - проблему мог создать кто-угодно."
    din_narrator "Разве что дальше всего от костра, на отшибе сегодняшнего праздника двое личностей о чём-то спорили."
    din_narrator "Слов отсюда услышать было нельзя, но было легко заметить у одного из участников характерно дрожащий подбородок и дергающийся нос, как у девчонки - стандартные для новых личностей признаки гнева и жажды неприятностей."
    din_third "Может, ты ими займешься?"
    din_teapot "О, нет, спасибо. Вон тот, что справа, вроде двадцатый в общем списке. Он, конечно, тупой, как пень, но моя челюсть от этого прочнее не становится."
    din_teapot "А тебе это погоды не сделает."
    din_third "Напомни мне научить тебя делать удобный компактный нож."
    din_narrator "Я ускорил шаг и пошел к сладкой парочке."
    din_narrator "За то время, что мы шли, эти двое уже стали кричать друг на друга. Пока внимания они не привлекали, но это временно."
    din_narrator "Моё желание улаживать детские разборки измерялось отрицательными числами, но статус обязывал."
    din_th "Сколько раз повторять: ЛЮБОЕ насилие сегодня запрещено! Но нет, всега найдется новичок, который посчитает себя выше правил!"   
    show din_pi silhouette normal at left as din_silhouette1
    show din_pi silhouette normal at right as din_silhouette2
    with dissolve
    din_narrator "Я остановился за пять шагов от них, но меня всё равно не заметили. Шорох за спиной подсказывал, что Чайник не отстал."
    din_pi1 "Еще раз что-то про неё скажешь и я за себя не ручаюсь."
    din_th "Как же неубедительно и безопасно звучал голос «левого». {w}И что это за расплывчатая угроза «за себя не ручаюсь»? Он в приступе злости в дерево врежется?"
    din_pi2 "Ты что, правда веришь в эту тупость? {w}Послушай, твоя ненаглядная с каждым, кого ты сегодня мог встретить, «уединялась» минимум раза..."
    stop ambience fadeout 2
    stop sound_loop fadeout 2
    play sound din_portal_use
    scene bg ext_polyana_night
    show din_pi silhouette normal at left as din_silhouette1
    show din_pi silhouette normal at right as din_silhouette2
    with flash
    play ambience ambience_forest_night fadein 2
    play music din_argsound_night fadein 2
    din_th "Отлично, свидетелей теперь нет."
    din_narrator "Неожиданная смена обстановки заставила их остановить разговор."
    din_narrator "Теперь они, наконец, изволили меня заметить."
    din_third "Прекращайте спор. {w}Продолжите на следующей неделе."
    din_narrator "Но эти двое плохо понимали, что сейчас произойдёт."
    din_pi2 "А не пошёл бы ты отсюда? Не лезь, если не хочешь получить!"
    din_narrator "Левый, судя по всему, думал то же самое."
    din_th "Почему в крохотном черепе этих одарённых нет хоть одной пары нейронов, которая бы связала их мгновенное перемещение и меня? {w}Это ведь несложно!"
    din_narrator "Но угроза Правого звучала убедительнее. {w}Чуть-чуть."
    din_th "Ладно, время разговоров вышло."
    din_narrator "Я совершил рваный рывок к Левому, увернувшись от двух несмелых взмахов руками - он, очевидно, ждал еще болтовни."
    din_narrator "Подобравшись достаточно близко, я одновременно поддел его ногу своим носком и приготовил удар локтем в корпус, чтобы сбить с ног и приложить о землю."
    din_narrator "Велико же было моё удивление, когда мой аккуратный манёвр прервался внезапным ударом со спины."
    din_narrator "Сила удара была невелика, но я всё же отбежал в сторону, чтобы оценить происходящее."
    din_narrator "Левый только оступился, а не упал, как я надеялся. Правый спокойно стоял рядом с ним."
    din_narrator "Я был частично удивлён. В первую очередь, не ждал, что его друг так быстро начнёт действовать, а во вторую - был уверен, что тот не будет вмешиваться, пока его недавнего оппонента не втопчут в грязь."
    din_th "Впрочем, они вполне могут препираться прямо сейчас."
    din_narrator "Двое пионеров передо мной молча переглянулись и начали расходиться в стороны."
    din_th "Сначала показалось, что они идут подальше друг от друга. {w}Так и было, но вдруг я понял зачем."
    din_th "Они брали меня с двух сторон в кольцо."
    din_th "Здесь явно было что-то не так."
    din_th "Двое Пионеров после спора не сговариваясь, работали вместе и теперь - слаженно."
    din_th "Это не было сверхсложно в принципе, но не для Пионеров. Каждый был эгоистом, не важно, новичок или долгожитель. {w}Все всегда предпочитали действовать в одиночку."
    din_th "Исключениями были разве что заранее спланированные события, вроде командых игр или взаимопомощи, но даже тогда каждый тянул одеяло на себя, не полагаясь лишний раз на другого."
    din_th "Хм, я слишком сильно озадачен. {w}Значит, где-то ошибаюсь."
    din_narrator "Оба теперь снова стояли справа и слева от меня, за пять метров каждый. Отвернусь полностью в одну сторону - оставлю вторую неприкрытой."
    din_narrator "Левый взял в руку камень для броска. {w}Хороший трюк, я его запомню."
    din_narrator "Правый ничего не подбирал, только встал в стойку для рывка."
    din_narrator "Он-то меня беспокоил больше. Если Левый очевидно был просто импульсивным и этим его тактика ограничивалась, то Правый был явно опаснее."
    din_narrator "Первый сразу бросит камень и ринется напролом, если я повернусь к нему спиной, но что сделает второй, я пока не мог сказать."
    din_th "Что бы я сделал на его месте?"
    din_narrator "Ответ пришел сам собой."
    din_narrator "Я бы дождался, пока моего напарника вырубят и разобрался бы сам."
    din_narrator "Теперь, когда Правша уже один раз прикрыл соратника, у последнего больше не было оправданий своей слабости. Так, вспоминать ему это будет на порядок унизительнее."
    din_th "А ведь это действительно прекрасный план."
    din_narrator "Отлично. Я снова почувствовал землю под ногами."
    din_th "Поехали."
    din_narrator "На предельной скорости я метнулся к слабаку. Перехватив обе его руки своими, я провёл удар лбом и отбросил его назад."
    din_narrator "Противник наконец-то потерял равновесие и выронил камень."
    din_narrator "Схватив новое оружие, я метнул его прямо под дых, сбивая немногие остатки воздуха."
    din_narrator "Сразу после этого я пригнулся от возможного удара второго."
    din_th "Теперь посмотрим что..."
    stop music fadeout 2
    play sound sfx_punch_washstand
    stop ambience fadeout 2
    $ din_blink(2)
    $ renpy.pause(3, hard=True)
    play ambience ambience_forest_night fadein 2
    play sound sfx_face_slap
    $ din_unblink('bg din_ext_polyana_night_blurred', 2)
    din_narrator "Спина мёрзнет. Как бы не простыть."
    din_narrator "Стоп, что произошло?"
    din_narrator "Кто-то назойливо хлопал ладонями по моим щекам."
    scene bg ext_polyana_night
    show din_hall pos2 smile2 at center
    with dissolve
    din_teapot "Вставай-вставай-вставай-вставай."
    din_th "Да что за!.."
    din_narrator "После того, как я рывком перешел в сидячее положение, ощутились изменения."
    din_narrator "А конкретно - новая форма моей головы."
    din_th "Похоже, образовался немаленький синяк. И, судя по липкости головы..."
    din_teapot "А, очнулся!"
    din_narrator "Рядом со мной лежала тяжелая ветка с небольшим пятном крови."
    din_narrator "Чайник, заметив как я пришел в сознание, перестал хлопать по моим щекам."
    din_teapot "Ты пробовал на разборках вспоминать обо мне? Если что, лагерей много и нормальные люди перемещаются по ним медленно. {w}В следующий раз искать тебя не буду."
    show din_hall pos2 smile with dspr
    din_teapot "Из-за этого я пропустил, как тебя вздули! Такое не каждую Неделю Зимы бывает, знаешь ли! {w}Ты хоть запомнил, что произошло? {w}Мне же интересно!"
    din_third "Тише, Чайник, и так голова трещит."
    din_narrator "Я аккуратно провёл пальцами по эпицентру боли на затылке. Рана уже практически не кровоточила, затянулась."
    din_teapot "Отвыкай от этого имени. Неделя Зимы заканчивается, скоро его поменяю. {w}Но что бы такое придумать..."
    din_th "Нужно как-то спрятать эту рану на голове. Если кто-то меня узнает - проблем станет еще больше."
    din_teapot "Шум, Шарф, Шпала, Шуруп... {w}Всё не то! Хотя нет, Шарф мне нравится, пока обдумаю."
    din_teapot "А, да. Держи!"
    din_narrator "Мне в руки попал обвязаный кругом красный галстук, теперь напоминающий бандану. {w}Судя по тому, что галстук Чайника был на своём стабильном месте, бандану он у кого-то отобрал."
    din_teapot "Тебе повезло так быстро очухаться. Обычно после таких историй валяются около восьми часов."
    din_third "Сколько я проспал?"
    din_teapot "Минут пятнадцать. Мне надоело ждать."
    din_narrator "Я многозначно глянул на него."
    din_th "Ну пускай."
    din_narrator "Повязка отлично закрывала рану. В ней я был похож на идиота, но сегодня общий праздник - идиотов много."
    din_teapot "Идём? {w}Как тебе имя Шаррум-итер? Достаточно пафосно или ему чего-то не хватает?"
    stop ambience fadeout 2
    scene bg black with Dissolve(2)
    $ renpy.pause(1, hard=True)
    scene bg din_fireplace_winterlong_anim with Dissolve(2)
    play sound_loop din_voices fadein 2
    play ambience sfx_forest_fireplace fadein 2
    play music din_god_is_an_astronaut_falling_leaves fadein 5
    din_narrator "Я устроил себе место на покрывале, недалеко от главного костра. Достаточно близко, чтобы не замёрзнуть, достаточно далеко, чтобы свет не выхватывал меня из общей массы."
    din_narrator "По мере того, как Неделя Зимы приближалась к концу, все меньше и меньше оставалось мест у костра."
    din_narrator "Все уже обсудили что хотели."
    din_narrator "Сейчас наступала почти ритуальная часть. {w}Все молча садились и смотрели в костёр."
    din_narrator "Каких-то особых правил на этот счет не было. {w}Просто сложилось, что в конце каждой такой смены все хотели подумать о своём, и нет для этого лучше места, чем рядом с костром и единомышленниками."
    din_narrator "Первая фигура легла, задремав на своём месте."
    play sound sfx_bush_body_fall
    $ renpy.pause(0.7, hard=True)
    scene bg din_stars_bush_anim with fade
    din_narrator "Захотелось последовать её примеру."
    din_th "В конце концов, сегодня был утомительный день."
    din_narrator "Я не забыл про тёх двоих. Уже был четкий план, как привязать Левого к дереву вверх ногами часов на шесть. Но что делать с Правшой, я пока что не решил."
    din_narrator "Гораздо важнее была их нетипичная кооперация. Просто совпадение?"
    din_th "Но, если это первый звоночек социальных перемен, то грядёт интересное время."
    din_th "С другой стороны..."
    din_narrator "Я устало вдохнул воздух."
    din_th "...это всё позже, не сейчас."
    din_th "Сейчас просто отдохну. И пусть мне не приснится ни один сон."
    stop music fadeout 4
    stop ambience fadeout 4
    stop sound_loop fadeout 4
    scene bg black with Dissolve(2)
    $ persistent.din_flags['din_hall_info_received'] = True
    $ renpy.pause(2, hard=True)
    jump din_winterlong_story_interlude
    
label din_winterlong_story_interlude:
    $ renpy.block_rollback()
    $ din_interlude_intro('Пробуждение')
    scene bg din_ext_camp_plain_sight_sunset with Dissolve(2)
    play ambience ambience_ext_road_evening fadein 2
    play music din_if_these_trees_could_talk_barren_lands_of_the_modern_dinosaur fadein 5
    din_th "Это место выводит из привычного равновесия."
    din_th "Я испытываю определённую... {w}Злость? {w}Или это безразличие? {w}Бессилие?"
    din_th "Но ведь эмоции испытываю я. {w}Точно ли лагерь является их причиной?"
    din_th "Или я сам — своя главная проблема?"
    din_narrator "Передо мной с закрытыми глазами лежал второй и последний житель этого места."
    din_th "Он никогда не проснется. {w}Без моей помощи."
    din_th "За всё время, что мы здесь, ничего не изменилось."
    din_th "{b}Ничего.{/b}"
    din_th "Небо всё того же цвета, тени той же длины."
    din_th "Даже мы сами. Мы не захотели ни есть, ни спать, не устали ни капли."
    din_th "Он был прав. {w}Здесь попросту не от чего проснуться."
    din_th "В этом и была его просьба — разбудить через сорок два часа."
    din_th "Думаю, в конце концов мы оба уснем, чтобы избавиться от голоса в своей голове."
    din_th "Что не говори, но, похоже, даже для нас вечность может быть неприятной."
    din_narrator "Я отошел от спящего тела."
    din_narrator "Трава сгибалась под моими ногами."
    din_th "Как ни странно, но больше всего я боялся вытоптать крохотный участок растительности в этой пустоши."
    din_th "Тогда бы образовалась крохотная несовершенность на необьятном полотне, которая мозолила бы мне глаза своим существованием всю вечность."
    din_th "Мог бы отвернуться, но не забыть её."
    din_th "Вторым выходом было вытоптать всю траву в этой утопии."
    din_th "Хороший вариант. {w}С условием, что у этой равнины есть край."
    $ din_blink(2)
    din_th "А что, если ничего из этого не существует?"
    din_th "Ни солнца, ни равнины, ни травы, ни меня?"
    din_th "Как это понять?"
    din_th "Как проверить, что хотя бы я сам когда-то был?"
    din_narrator "Я больше не чувствовал ни порывов ветра, ни запаха травы."
    din_narrator "Эта мысль дарила чувство внутреннего спокойствия."
    din_th "Если меня не существует, то и не нужно о чем-то беспокоиться, думать о будущем, планировать наперёд..."
    din_narrator "Любопытно, если меня, или того, что я собой считаю, нет, то что тогда есть?"
    din_narrator "Что задаёт вопросы? Что сейчас прогоняет эти мысли в своей голове?"
    $ din_unblink("bg din_ext_camp_plain_sight_sunset", 2)
    din_narrator "Это красивое место. {w}Красивое, но пустое."
    din_narrator "Ладно, хватит бездельничать."
    din_narrator "Пошла уже двадцать пятая сотня минут. {w}Кому-то пора просыпаться."
    stop music fadeout 2
    stop ambience fadeout 2
    scene bg black with Dissolve(2)
    $ persistent.din_flags['din_winterlong_story_completed'] = True
    $ renpy.pause(2, hard=True)
    jump din_rolegame_story